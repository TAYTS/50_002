module mojo_top (
    input clk,              // 50MHz clock
    input rst_n,            // reset button (active low)
    output led [8],         // 8 user controllable LEDs
    input cclk,             // configuration clock, AVR ready when high
    output spi_miso,        // AVR SPI MISO
    input spi_ss,           // AVR SPI Slave Select
    input spi_mosi,         // AVR SPI MOSI
    input spi_sck,          // AVR SPI Clock
    output spi_channel [4], // AVR general purpose pins (used by default to select ADC channel)
    input avr_tx,           // AVR TX (FPGA RX)
    output avr_rx,          // AVR RX (FPGA TX)
    input avr_rx_busy,      // AVR RX buffer full
    output io_led [3][8],   // LEDs on IO Shield
    output io_seg [8],      // 7-segment LEDs on IO Shield
    output io_sel [4],      // Digit select on IO Shield
    input io_button [5],    // 5 buttons on IO Shield
    input io_dip [3][8],    // DIP switches on IO Shield
    output a,               // Output signal A for full adder [P51]
    output b,               // Output signal B for full adder [P41]
    output c,               // Output signal C for full adder [P35]
    input raw_s,            // Input signal Sum from full adder   [P33]
    input raw_c             // Input signal Cout from full adder  [P30]
  ) {
  
  sig rst;    // reset signal
  sig rst1;
  .clk(clk) {
    // The reset conditioner is used to synchronize the reset signal to the FPGA
    // clock. This ensures the entire FPGA comes out of reset at the same time.
    reset_conditioner reset_cond;
    
    // Used to filter the center button signal
    button_conditioner button_cond;

    .rst(rst) {
      display_status seg;      
      dff mode[1];  // Define the testing mode 
    }
    
    dff ctr[3];  // counter for test_cases index

    // Use center button to reset FSM
    .fsm_rst(rst1) {
      adder_fsm adder_fsm;
    }
  }     

  test_cases test_cases;
  manual_checker manual_checker;

  always {
    button_cond.in = io_button[1];  // filter the center button signal

    //// Filter the reset button ////
    reset_cond.in = ~rst_n;  // input raw inverted reset signal
    rst = reset_cond.out;    // conditioned reset
    
    rst1 = button_cond.out;  // manual reset signal
    
    //// Pre-set value for unused pin-out ////
    led = 8h00;             // turn LEDs off
    spi_miso = bz;          // not using SPI
    spi_channel = bzzzz;    // not using flags
    avr_rx = bz;            // not using serial port
    //////////////////////////////////////////

    
    //// Define the testing and display mode ////
    //// 1: Auto, 0: Manual                  ////
    mode.d = io_dip[2][7];
    /////////////////////////////////////////////
    
    //// Define the input for FSM module  and output of pinout ////
    /////////////////////////////////////////
    //// increment the test_cases using counter ////
    test_cases.idx = ctr.q;
    ////////////////////////////////////////////////
    //// mode: 1 => use the automated test_cases //////////
    if (mode.q) {
      //// pinout output ////
      a = test_cases.values[0];
      b = test_cases.values[1];
      c = test_cases.values[2];
      //// adder_fsm input ////
      adder_fsm.a = test_cases.values[0];
      adder_fsm.b = test_cases.values[1];
      adder_fsm.c = test_cases.values[2];
    }
    //// mode: 0 => manually toggle the group 1 switch ////
    else {
      //// pinout output ////
      a = io_dip[0][0];
      b = io_dip[0][1];
      c = io_dip[0][2];
      //// adder_fsm input ////
      adder_fsm.a = 1b0;
      adder_fsm.b = 1b0;
      adder_fsm.c = 1b0;  
    }
    ///////////////////////////////////////////////////////

    //// Provide actual and raw data for adder_fsm checker ////
    if (mode.q) {
      // Set the raw for FSM
      adder_fsm.raw = c{raw_c, raw_s};
      // Set 0 for manual_check
      manual_checker.raw = 2b0;
      manual_checker.expect = 2b0;
    } else {
      // Set 0 for FSM
      adder_fsm.raw = 2b0;
      //adder_fsm.expect = 2b0;

      // Set the raw and expect vlue for manual_checker
      manual_checker.raw = c{raw_c, raw_s};
      if ((io_dip[0][0] + io_dip[0][1] + io_dip[0][2]) == 3) {
        manual_checker.expect = 2b11;  // State: CS
      } else if ((io_dip[0][0] + io_dip[0][1] + io_dip[0][2]) == 2) {
        manual_checker.expect = 2b10;  // State: C
      } else if ((io_dip[0][0] + io_dip[0][1] + io_dip[0][2]) == 1) {
        manual_checker.expect = 2b01;  // State: S
      } else {
        manual_checker.expect = 2b00;  // State: IDLE
      }
    }
    //////////////////////////////////////////////////////////

    
    //// Display out comparison (FAIL) ////
    if (mode.q) {
      led[1:0] = adder_fsm.pass;
      if (adder_fsm.pass == 2b00) {
        // group 1 LED: inputs for the full bit adder
        io_led[0] = adder_fsm.input_ref;
      
        // group 2 LED: expected output
        io_led[1] = test_cases.values[4:3];
      
        // group 3 LED: actual output
        io_led[2] = adder_fsm.err_ref;
      } else {
        // if everthing pass, NO LED
        io_led = 3x{{8h00}};
      }
    } else {
      if (manual_checker.pass == 2b00) {
        // group 1 LED: inputs for the full bit adder
        io_led[0] = c{io_dip[0][2], io_dip[0][1], io_dip[0][0]};
      
        // group 2 LED: expected output
        io_led[1] = manual_checker.ans_ref;
      
        // group 3 LED: actual output
        io_led[2] = c{raw_c, raw_s};
      } else {
        // if everthing pass, NO LED
        io_led = 3x{{8h00}};
      }
    }
    ///////////////////////////////////////
    
    
    //// Manual reset the test cases counter ////
    if (rst1) {
        ctr.d = 3b000;
    }
    ////////////////////////////////////////////

    // Auto mode
    if (mode.q) {
        led[7:5] = ctr.q;
        led[4:3] = adder_fsm.verify;
        // Display the test cases status
        seg.status = adder_fsm.pass;
        //// if the test case is PASS, go to next counter
        if (~rst1) {
          if (adder_fsm.pass == 2b01) {
            if (ctr.q < 7) {      
                ctr.d = ctr.q + 1;
            }    
          } else {
            ctr.d = ctr.q;
          }
        }
    } else {
    // Manual mode: Use manual checker
      seg.status = manual_checker.pass; 
    }
    
    // Display the checking status
    io_seg = ~seg.seg;
    io_sel = ~seg.sel;    
  }
}